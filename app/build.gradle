plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "de.libexample"
        minSdk 26
        targetSdk 32
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

/**
 * All bundled dependencies will be merged into the FAT-JAR bundled-dependencies.
 * The packages will be relocated according to the rules specified in bundleDependencies task.
 */
configurations {
    internalized
}

task internalizeDependencies(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    archiveBaseName = 'internalized-deps'
    version = ''
    configurations = [project.configurations.internalized]

    relocate 'com.google.gson', 'de.libexample.gson'
}
preBuild.dependsOn += internalizeDependencies

dependencies {
    internalized 'com.google.code.gson:gson:2.8.8'
    implementation files('build/libs/internalized-deps.jar')

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
}